apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'
group 'net.codysehl'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/recyclerview-list-adapter-release.aar")
            groupId
            artifactId 'recyclerview-list-adapter'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = "lalunamel"
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'net.codysehl'
        name = ''
        description = "An example of using the bintray plugin with gradle plugin 3.0.0"
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/lalunamel/RecyclerViewListAdapter'
        dryRun = true
        version {
            name = this.version
            desc = "Example ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}